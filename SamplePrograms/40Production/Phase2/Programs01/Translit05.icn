# This program uses the alphabet table to convert a Strong's Hebrew Lexicon
# to a common transliteration so it can be easily read. The Hebrew coding of the input file must be
# "Hebrew (Windows-1255) Encoded Text." You can create such a file easily with the Libre Office 
# "Save As" option.
# Note: When creating a conversion table, "Don't forget the five Hebrew final forms!" Also, any
#       unwanted characters (like vowells or punctuation) can be converted to the empty string.
global alphaCset, alphabet, file02
procedure main()
# Initializations
  file01 := open("WorkingOut01.txt","r") | stop("Unable to open WorkingOut01.txt.")
  file02 := open("WorkingOut02.txt","w") | stop("Unable to open WorkingOut02.txt.")

# Create the empty Alphabet Table
  alphabet := table("")

# Load the Alphabet Table.
  alphabet["\xC0"] := "" # Begin Vowels
  alphabet["\xC1"] := "" # Sheva
  alphabet["\xC2"] := "" # Hataf Segol
  alphabet["\xC3"] := "" # Hataf Patah
  alphabet["\xC4"] := "" # Hataf Qamats
  alphabet["\xC5"] := "" # Hiriq
  alphabet["\xC6"] := "" # Tsere
  alphabet["\xC7"] := "" # Segol
  alphabet["\xC8"] := "" # Patah
  alphabet["\xC9"] := "" # Qamats
  alphabet["\xCA"] := "" # Holam
  alphabet["\xCB"] := "" # Qubuts
  alphabet["\xCC"] := "" # Dagesh or Mapiq
  alphabet["\xCD"] := "" # Meteg
  alphabet["\xCE"] := "" # Maqaf
  alphabet["\xCF"] := "" # Rafe
  alphabet["\xD0"] := "" # Paseq
  alphabet["\xD1"] := "" # Shin Dot
  alphabet["\xD2"] := "" # Sin Dot
  alphabet["\xD3"] := "" # Sof Pasuq (colon)
  alphabet["\xD4"] := "" # Yiddish Double Vav
  alphabet["\xD5"] := "" # Vav Yod
  alphabet["\xD6"] := "" # Double Yod
  alphabet["\xD7"] := "" # Geresh
  alphabet["\xD8"] := "" # Gershayim
  alphabet["\xE0"] := "A" # Begin Consonants: Aleph
  alphabet["\xE1"] := "B" # Beth
  alphabet["\xE2"] := "G" # Gimel
  alphabet["\xE3"] := "D" # Daleth
  alphabet["\xE4"] := "H" # He
  alphabet["\xE5"] := "V" # Vav
  alphabet["\xE6"] := "Z" # Zayin
  alphabet["\xE7"] := "X" # Heth
  alphabet["\xE8"] := "+" # Teth
  alphabet["\xE9"] := "Y" # Yod
  alphabet["\xEA"] := "K" # Final Kaf
  alphabet["\xEB"] := "K" # Kaf
  alphabet["\xEC"] := "L" # Lamed
  alphabet["\xED"] := "M" # Final Mem
  alphabet["\xEE"] := "M" # Mem
  alphabet["\xEF"] := "N" # Final Nun
  alphabet["\xF0"] := "N" # Nun
  alphabet["\xF1"] := "$" # Samekh
  alphabet["\xF2"] := "@" # Ayin
  alphabet["\xF3"] := "P" # Final Pe
  alphabet["\xF4"] := "P" # Pe
  alphabet["\xF5"] := "C" # Final Tsade
  alphabet["\xF6"] := "C" # Tsade
  alphabet["\xF7"] := "Q" # Qoph
  alphabet["\xF8"] := "R" # Resh
  alphabet["\xF9"] := "S" # Sin/Shin
  alphabet["\xFA"] := "T" # Tav
#  alphabet["\xFD"] := "" # Left to Right Mark
#  alphabet["\xFE"] := "" # Right to Left Mark

# Write the content of the alphabet table to the console.
  every a := key(alphabet) do
    write(file02,a,":",alphabet[a])

# Create a cset containing the letters in alphabet.
  alphaString := ""
  every a := key(alphabet) do
    alphaString ||:= a
  alphaCset := cset(alphaString)
  write(file02,"alphabetCset ==",alphaString,"== Length = ",*alphaString)


# Convert file01 in it's own code set (Windows-1255) to file02 in the common code set.
  while lineIn := read(file01) do {
    strongsNumber := lineIn[1:10]
    lineIn := lineIn[10:0]
    lineIn := translit(lineIn)
    write(file02,strongsNumber,lineIn)
  }
end

procedure translit(lineIn)
  lineOut := ""
  while *lineIn > 0 do { 
    partA := partB := partC := partD := ""
    lineIn ? {
      partA := tab(upto(alphaCset)) # Save leading non-Hebrew text.
      if (partB := tab(many(alphaCset))) then { # Save Hebrew word.
        partC := translitHebrew(partB) # Transliterate the Hebrew Word.
        partD := tab(0) # Save the remainder -- the definition.
        lineOut ||:= partA || partC || " " || partB # Notice we saved the original Hebrew.
        lineIn := partD # Redo with shorter line.
      }
      else {
        lineOut ||:= lineIn
        return lineOut 
      }
    }
  }
end

procedure translitHebrew(hebrewText)
  transedHebrew := ""
  #write(file02,"hebrewText ==>",hebrewText,"<") #Diagnostic
  hebrewText ? {
    while (nextChar := move(1)) do {
      transedChar := alphabet[nextChar]
      transedHebrew ||:= transedChar
      #write(file02,"nextChar ==>",nextChar,"< transedChar ==>",transedChar,"< transedHebrew ==>",transedHebrew,"<")
    }
  }
return transedHebrew
end
