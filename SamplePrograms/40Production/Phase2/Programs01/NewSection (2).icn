# This program accepts command line arguments which delineate a section of scripture to be extracted
# from the Tanach for processing. The arguments are Book (three letters), Chapter (a number), and Verse (a number).
# The arguments occur twice indicating the starting and ending verses to be extracted.

procedure main(args)
  if /args | (*args < 6) then stop("Usage: NewSection.exe Book Chapter Verse Book Chapter Verse")
  startingBook    := args[1]
  startingChapter := args[2]
  startingVerse   := args[3]
  endingBook      := args[4]
  endingChapter   := args[5]
  endingVerse     := args[6]

  startLoc := startingBook || " " || ("00" || startingChapter)[-3:0] || ":" || ("00" || startingVerse)[-3:0]
  endLoc   := endingBook   || " " || ("00" ||   endingChapter)[-3:0] || ":" || ("00" ||   endingVerse)[-3:0]
  write("The Starting Location is ",startLoc,". The Ending Location is ",endLoc)

  file01 := open("00_Tanach_Lines.txt","r") | stop("Can't open 00_Tanach_Lines.txt.")

  while lineIn := read(file01) do {
    if lineIn[13:24] == startLoc then {write("Starting Location ",startLoc," found. File creation is starting.")
      startingLine := lineIn[1:6]
      break
    }
  }

  outFileName := startingLine || "aPaninSectionsInput" || startingBook || startingChapter || "_" || startingVerse || "-" ||
    endingBook || endingChapter || "_" || endingVerse || ".txt"
  file02 := open(outFileName,"w") | stop("Can't open ",outfileName,".")

  write(file02,"!Section: ",startLoc," thru ",endLoc)
  write(file02,"$",lineIn[13:0])
  write(file02,">>>",lineIn[25:0])

  while lineIn := read(file01) do {
    write(file02,"$",lineIn[13:0])
    write(file02,">>>",lineIn[25:0])
    if lineIn[13:24] == endLoc then {
      write("  Ending Location ",endLoc," found. Program is finished.")
      break
    }
  }
end
