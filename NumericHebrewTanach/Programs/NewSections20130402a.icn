# This program accepts command line arguments which delineate a section of scripture to 
# be extracted from the Tanach for processing. The arguments are Book (three letters), 
# Chapter (a number), and Verse (a number). The arguments occur twice indicating the 
# starting and ending verses to be extracted.

procedure main(args)
  if /args | (*args < 6) then 
    stop("Usage: NewSection.exe Book Chapter Verse Book Chapter Verse")
  startingBook    := args[1]
  startingChapter := args[2]
  startingVerse   := args[3]
  endingBook      := args[4]
  endingChapter   := args[5]
  endingVerse     := args[6]

  startLoc := startingBook || " " || ("00" || startingChapter)[-3:0] || ":" || 
                                     ("00" || startingVerse)[-3:0]
  endLoc   := endingBook   || " " || ("00" || endingChapter)[-3:0] || ":" || 
                                     ("00" || endingVerse)[-3:0]
  write("The Starting Location is ",startLoc,". The Ending Location is ",endLoc)
  outputFileOpen := 0

  file00 := open("TanachNames.txt","r") | stop("Can't open TanachNames.txt.")
  while fileIn := read(file00) do {
    file01 := open(fileIn,"r") | stop("Can't open ",fileIn,".")
    shortFileName := fileIn[-18:0]
    write("Processing:\n",fileIn,".")

    startingLine := "00000"
    while lineIn := read(file01) do {
      #write("lineIn == ",lineIn)
      if lineIn[13:24] == startLoc then {
        write("Starting Location ",startLoc," found. Section creation is starting.")
        startingLine := lineIn[1:6]
        break
      }
    }

    if outputFileOpen = 0 then {
      outFileName := "PaninSectionsInput" || startingBook || startingChapter || "_" || 
        startingVerse || "-" || endingBook || endingChapter || "_" || endingVerse ||
        ".txt"
      file02 := open(outFileName,"w") | stop("Can't open ",outfileName,".")
      outputFileOpen := 1
    }

    write(file02,"!File: ",shortFileName)
    write(file02,"!Section: ",startLoc," thru ",endLoc)
    write(file02,"$",lineIn[13:0])
    write(file02,">>>",lineIn[25:0])

    while lineIn := read(file01) do {
      write(file02,"$",lineIn[13:0])
      write(file02,">>>",lineIn[25:0])
      if lineIn[13:24] == endLoc then {
        write("  Ending Location ",endLoc," found. This Tanach is finished.")
        break
      }
    }
    close(file01)
  }
end
